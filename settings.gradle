pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('net.fabricmc.unpick')
                includeGroup('fabric-loom')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = uri('https://repo.spongepowered.org/repository/maven-public')
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Forge'
                    url = uri('https://maven.minecraftforge.net')
                }
            }
            filter {
                includeGroupAndSubgroups('net.minecraftforge')
            }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

// This should match the folder name of the project, or else IDEA may complain (see https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = 'multiloader-mods-mc'

// Define the loaders we support
// Note: Forge temporarily disabled due to lwjgl-freetype macOS issue in 1.21.11
def loaders = ['common', 'fabric', 'neoforge']

// Dynamically discover and include all mod subprojects under each loader
loaders.each { loader ->
    def loaderDir = new File(rootDir, loader)
    if (loaderDir.exists() && loaderDir.isDirectory()) {
        loaderDir.eachDir { modDir ->
            def modName = modDir.name
            
            // Skip build directories and hidden folders
            if (!modName.startsWith('.') && modName != 'build' && modName != 'src') {
                def projectPath = ":${loader}:${modName}"
                include(projectPath)
                project(projectPath).projectDir = modDir
            }
        }
    }
}