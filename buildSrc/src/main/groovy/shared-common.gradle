// This build.gradle is shared by all common subprojects
// Each mod subproject under common/ will automatically use this configuration

apply plugin: 'multiloader-common'
apply plugin: 'net.neoforged.moddev'

// Load mod-specific properties from the subproject's gradle.properties file
ext {
    // These will be loaded from each mod's gradle.properties
    modId = project.findProperty('mod_id') ?: rootProject.findProperty('mod_id')
    modName = project.findProperty('mod_name') ?: rootProject.findProperty('mod_name')
    modVersion = project.findProperty('version') ?: rootProject.findProperty('version')
    modDescription = project.findProperty('description') ?: rootProject.findProperty('description')
}

// Set the archive name based on mod-specific properties
base {
    archivesName = "${modId}-${project.parent.name}-${minecraft_version}"
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'common')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'common')
            }
        }
    }
}
